plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.6.0-SNAPSHOT"
description = "Expand Json"

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly("org.embulk:embulk-api:0.10.31")
    compileOnly("org.embulk:embulk-spi:0.10.31")
    compile("com.jayway.jsonpath:json-path:2.4.0") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    compile("org.embulk:embulk-util-config:0.3.1") {
        // They conflict with embulk-core:0.10.31. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile("org.embulk:embulk-util-json:0.1.1") {
        // They conflict with embulk-core:0.10.31. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"

        // It conflicts with embulk-api:0.10.31, and it is expected to stay in embulk-api.
        // It is just exluded here.
        exclude group: "org.msgpack", module: "msgpack-core"
    }
    compile("org.embulk:embulk-util-timestamp:0.2.1")

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.31.
    compile("com.fasterxml.jackson.core:jackson-annotations:2.6.7")
    compile("com.fasterxml.jackson.core:jackson-core:2.6.7")
    compile("com.fasterxml.jackson.core:jackson-databind:2.6.7")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7")
    compile("javax.validation:validation-api:1.1.0.Final")

    testImplementation "junit:junit:4.13"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "org.embulk:embulk-core:0.10.31:tests"
    testImplementation "org.embulk:embulk-core:0.10.31"

    // TODO: Remove them.
    // They are now required because the dependency libraries of them are behind sub ClassLoaders.
    // Including them in "testCompile" is a tentative workaround.
    testImplementation "org.embulk:embulk-deps:0.10.31"
}

test {
    jvmArgs '-Xms4g', '-Xmx4g', '-XX:MaxMetaspaceSize=1g'
    maxHeapSize = "4g"
}

embulkPlugin {
    mainClass = "org.embulk.filter.expand_json.ExpandJsonFilterPlugin"
    category = "filter"
    type = "expand_json"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://github.com/embulk/embulk-filter-expand_json"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Takahiro Nakayama"
                        email = "civitaspo@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-filter-expand_json.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-filter-expand_json.git"
                    url = "https://github.com/embulk/embulk-filter-expand_json"
                }
            }
        }
    }

    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Civitaspo" ]
    email = [ "civitaspo@gmail.com" ]
    summary = "Expand Json filter plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-filter-expand_json"
    licenses = [ "Apache-2.0" ]

    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE_GEM")
    rename ("NOTICE_GEM", "NOTICE")
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
