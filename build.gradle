plugins {
    id "com.github.kt3k.coveralls" version "2.10.1"
    id "jacoco"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "pro.civitaspo"
version = "0.4.0"
description = "Expand Json"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly("org.embulk:embulk-api:0.10.6")
    compileOnly("org.embulk:embulk-core:0.10.6")
    compile("com.jayway.jsonpath:json-path:2.4.0") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.5.
    compile("com.fasterxml.jackson.core:jackson-annotations:2.6.7")
    compile("com.fasterxml.jackson.core:jackson-core:2.6.7")
    compile("com.fasterxml.jackson.core:jackson-databind:2.6.7")

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-core:0.10.6:tests"
    testImplementation "org.embulk:embulk-core:0.10.6"

    // TODO: Remove them.
    // They are now required because the dependency libraries of them are behind sub ClassLoaders.
    // Including them in "testCompile" is a tentative workaround.
    testImplementation "org.embulk:embulk-deps-buffer:0.10.6"
    testImplementation "org.embulk:embulk-deps-config:0.10.6"
    testImplementation "org.embulk:embulk-deps-timestamp:0.10.6"
}

test {
    jvmArgs '-Xms4g', '-Xmx4g', '-XX:MaxMetaspaceSize=1g'
    maxHeapSize = "4g"
}

embulkPlugin {
    mainClass = "org.embulk.filter.expand_json.ExpandJsonFilterPlugin"
    category = "filter"
    type = "expand_json"
    ignoreConflicts = [
        // They conflict with embulk-core:0.10.6, but embulk-core will remove them from its dependencies.
        // They are intentionally included in the plugin's dependencies to keep it working before and after the removal.
        // Warning messages against them are marked to ignore explicitly here.
        [ group: "com.fasterxml.jackson.core", module: "jackson-annotations" ],
        [ group: "com.fasterxml.jackson.core", module: "jackson-core" ],
        [ group: "com.fasterxml.jackson.core", module: "jackson-databind" ],
    ]
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Civitaspo" ]
    email = [ "civitaspo@gmail.com" ]
    summary = "Expand Json filter plugin for Embulk"
    homepage = "https://github.com/civitaspo/embulk-filter-expand_json"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
